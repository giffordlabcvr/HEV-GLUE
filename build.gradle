apply plugin: 'war'

apply plugin: 'org.hidetake.ssh'

project.ext.set("glueVersion", "0.1.151")

remotes {
  gluehome_aws {
    host = '52.16.113.119'
    user = 'ubuntu'
    identity = file('/Users/joshsinger/.ssh/gluehome-aws.pem')
  }
  
}

repositories {
	mavenLocal()
}

configurations {
	gluetoolsWebWar
	gluetoolsWsWar
}

dependencies {
    gluetoolsWebWar group: 'cvr.ac.uk', name: 'gluetools-web', version: project.glueVersion, transitive: false
    gluetoolsWsWar group: 'cvr.ac.uk', name: 'gluetools-ws', version: project.glueVersion, transitive: false
}


buildscript {
  repositories {
    // for ssh-deploy stuff
    jcenter()
  }
  dependencies {
    classpath group: 'org.hidetake', name: 'gradle-ssh-plugin', version: '1.1.3'
    classpath group: 'commons-io', name: 'commons-io', version: '1.3.2'
    
  }
}

war {
	webAppDirName = 'WebContent'
	archiveName 'hevGlue.war'
}

task deployHevWebAppWarGlueHome(dependsOn: ['war']) {
  ext.glueHevWebAppWarFile = new File(buildDir, '/libs/hevGlue.war')
  ext.glueHevWebAppWarTouchfile = new File(buildDir, '/deployHevWebAppWarGlueHome.touchfile')
  inputs.file glueHevWebAppWarFile
  outputs.file glueHevWebAppWarTouchfile
  doLast {
	  println "Uploading "+glueHevWebAppWarFile.name
	  ssh.run {
	    session(remotes.gluehome) {
	      put from: glueHevWebAppWarFile.absolutePath, into: '/tmp/hevGlue.war'
	      execute 'sudo -n systemctl stop tomcat'
	      execute 'sudo -n rm -rf /usr/share/tomcat/webapps/hevGlue'
	      execute 'sudo -n rm -rf /usr/share/tomcat/webapps/hevGlue.war'
	      execute 'sudo -n mv /tmp/hevGlue.war /usr/share/tomcat/webapps'
	      execute 'sudo -n rm -rf /var/cache/tomcat/work/Catalina/localhost/hevGlue'
	      execute 'sudo -n systemctl start tomcat'
	    }
	  }
	  glueHevWebAppWarTouchfile.delete()
	  buildDir.mkdirs()
	  glueHevWebAppWarTouchfile.createNewFile()
  }
}

task copyGluetoolsWebWar(type: Copy) {
    from configurations.gluetoolsWebWar
    into "$buildDir/runtimeWars"
    rename('gluetools-web-'+project.glueVersion+'.war', 'gluetools-web.war')
}

task copyGluetoolsWsWar(type: Copy) {
    from configurations.gluetoolsWsWar
    into "$buildDir/runtimeWars"
    rename('gluetools-ws-'+project.glueVersion+'.war', 'gluetools-ws.war')
}


task deployAll(dependsOn: ['deployGluetoolsWebWarGlueHomeAWS',
	'deployGluetoolsWsWarGlueHomeAWS',
	'deployHevGlueWarGlueHomeAWS',
	'deployHevGlueGluetoolsWsContextXmlGlueHomeAWS',
	'deployHevGlueConfigGlueHomeAWS',
	'deployHevGlueDbToGlueHomeAWS'])

task deployGluetoolsWebWarGlueHomeAWS(type: DeployWarTask, dependsOn: 'copyGluetoolsWebWar') {
    warFile = new File(buildDir, 'runtimeWars/gluetools-web.war')
    remote = remotes.gluehome_aws
    taskBuildDir = buildDir
	warBaseName = org.apache.commons.io.FilenameUtils.getBaseName(warFile.name)
    touchFile = new File(taskBuildDir.name, '/deploy.'+warBaseName+'.'+remote.name+'.touchfile')
    inputs.file warFile
    outputs.file touchFile
    taskSsh = ssh
}

task deployGluetoolsWsWarGlueHomeAWS(type: DeployWarTask, dependsOn: 'copyGluetoolsWsWar') {
    warFile = new File(buildDir, 'runtimeWars/gluetools-ws.war')
    remote = remotes.gluehome_aws
    taskBuildDir = buildDir
	warBaseName = org.apache.commons.io.FilenameUtils.getBaseName(warFile.name)
    touchFile = new File(taskBuildDir, '/deploy.'+warBaseName+'.'+remote.name+'.touchfile')
    inputs.file warFile
    outputs.file touchFile
    taskSsh = ssh
}

task deployHevGlueWarGlueHomeAWS(type: DeployWarTask, dependsOn: war) {
    warFile = new File(buildDir, '/libs/hevGlue.war')
    remote = remotes.gluehome_aws
    taskBuildDir = buildDir
	warBaseName = org.apache.commons.io.FilenameUtils.getBaseName(warFile.name)
    touchFile = new File(taskBuildDir, '/deploy.'+warBaseName+'.'+remote.name+'.touchfile')
    inputs.file warFile
    outputs.file touchFile
    taskSsh = ssh
}

class DeployWarTask extends DefaultTask {
	File warFile
	org.hidetake.groovy.ssh.core.Remote remote
	File taskBuildDir
	String warBaseName
    File touchFile
    org.hidetake.groovy.ssh.core.Service taskSsh

    @TaskAction
    def deploy() {
		  println "Stopping Tomcat"
		  taskSsh.run {
		    session(remote) {
		      execute 'sudo service tomcat7 stop; sleep 10s'
		      execute 'while true; do tcresult="$(sudo service tomcat7 status)" ; if [[ $tcresult != *"Tomcat servlet engine is running"* ]] ; then break ; fi ; sleep 0.5 ; echo "Tomcat is still running" ; done'
		      execute 'sleep 5'
		    }
		  }
		  println "Uploading "+warFile
		  taskSsh.run {
		    session(remote) {
		      put from: warFile.absolutePath, into: '/tmp/'+warFile.name
		      execute 'sudo mkdir -p /var/lib/tomcat7/webapps_hev'
		      execute 'sudo chmod a+rwx /var/lib/tomcat7/webapps_hev'
		      execute 'sudo rm -rf /var/lib/tomcat7/webapps_hev/'+warBaseName
		      execute 'sudo rm -rf /var/lib/tomcat7/webapps_hev/'+warFile.name
		      execute 'sudo mv /tmp/'+warFile.name+' /var/lib/tomcat7/webapps_hev/'+warFile.name
		      execute 'sudo rm -rf /var/lib/tomcat7/work/Catalina/hev.glue.cvr.ac.uk/'+warBaseName
		      execute 'sudo rm -rf /var/cache/tomcat7/Catalina/hev.glue.cvr.ac.uk/'+warBaseName
		      
		    }
		  }
		  println "Starting Tomcat"
		  taskSsh.run {
		    session(remote) {
		      execute 'sudo service tomcat7 start'
		    }
		  }
		  touchFile.delete()
		  taskBuildDir.mkdirs()
		  touchFile.createNewFile()
	}
}



// config for HEV-GLUE gluetools-ws webapp 
task deployHevGlueGluetoolsWsContextXmlGlueHomeAWS() {
  ext.file = new File(projectDir, '/gluehome_aws/gluetools-ws-context.xml')
  ext.touchfile = new File(buildDir, '/deployGlueWSConfigGlueHomeAWS.touchfile')
  inputs.file file
  outputs.file touchfile
  doLast {
	  println "Uploading "+file.name
	  ssh.run {
	    session(remotes.gluehome_aws) {
	      put from: file.absolutePath, into: '/tmp/gluetools-ws.xml'
	      execute 'sudo service tomcat7 stop; sleep 10s'
	      execute 'sudo mkdir -p /var/lib/tomcat7/conf/Catalina/hev.glue.cvr.ac.uk'
	      execute 'sudo mv /tmp/gluetools-ws.xml /var/lib/tomcat7/conf/Catalina/hev.glue.cvr.ac.uk/gluetools-ws.xml'
	      execute 'sudo service tomcat7 start'
	    }
	  }
	  touchfile.delete()
	  buildDir.mkdirs()
	  touchfile.createNewFile()
  }
}

// config for HEV-GLUE glue engine which tells it where to find its database. 
task deployHevGlueConfigGlueHomeAWS {
  ext.file = new File(projectDir, '/gluehome_aws/hev-glue-gluetools-config.xml')
  ext.touchfile = new File(buildDir, '/deployHevGlueConfigGlueHomeAWS.touchfile')
  inputs.file file
  outputs.file touchfile
  doLast {
	  println "Uploading "+file.name
	  ssh.run {
	    session(remotes.gluehome_aws) {
	      execute "sudo mkdir -p /opt/gluetools/conf"
	      put from: file, into: '/tmp'
	      execute "sudo mv /tmp/hev-glue-gluetools-config.xml /opt/gluetools/conf/"
	    }
	  }
	  touchfile.delete()
	  buildDir.mkdirs()
	  touchfile.createNewFile()
  }
}

task initHevGlueDBGlueHomeAWS {
  doLast {
	  ssh.run {
	    session(remotes.gluehome_aws) {
			// create user gluetools with correct grants
			execute "echo 'GRANT ALL ON HEV_GLUE.* TO \"hevglue\"@\"localhost\" identified by \"hevglue\";' | mysql -u root --password=root123"
			// create HEV_GLUE database
			execute "echo 'create database HEV_GLUE character set UTF8;' | mysql -u hevglue --password=hevglue" 
	    }
	  }
  }
}

task dumpHevGlueDB(type: Exec) {
	standardOutput = new FileOutputStream(new File('/tmp/hev_glue.sql'))
	commandLine '/usr/local/mysql/bin/mysqldump', '-u', 'hevglue', '--password=hevglue', 'HEV_GLUE'
}

task zipHevGlueDB(type: Exec, dependsOn: 'dumpHevGlueDB') {
	commandLine '/usr/bin/gzip', '-f', '/tmp/hev_glue.sql'
}

task deployHevGlueDbToGlueHomeAWS(dependsOn: 'zipHevGlueDB') {
  doLast {
	  println "Uploading HEV-GLUE DB"
	  ssh.run {
	    session(remotes.gluehome_aws) {
	 	  put from: '/tmp/hev_glue.sql.gz', into: '/tmp/hev_glue.sql.gz'
	      execute 'echo "drop database HEV_GLUE; create database HEV_GLUE character set UTF8;" | mysql -u hevglue --password=hevglue HEV_GLUE'
	      execute 'gunzip -c /tmp/hev_glue.sql.gz | mysql -u hevglue --password=hevglue HEV_GLUE'
	      execute 'rm /tmp/hev_glue.sql.gz'
	    }
	  }
  }
}









